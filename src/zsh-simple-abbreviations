#!/usr/bin/env zsh

KEY_REGEX="^[[:alnum:]]+$"

if [[ $# -eq 0 ]]; then
	echo "zsh_simple_abbreviations no sub-command or arguments provided."
	return 1
fi

case $1 in
--set)
	if [[ $# -ne 3 ]]; then
		echo "zsh_simple_abbreviations set sub-command requires a key and value."
		return 1
	fi

	local key=${2}
	local value=${3}

	if [[ ${key} =~ ${KEY_REGEX} ]]; then
		ZSH_SIMPLE_ABBREVIATIONS[$key]="${value}"
	else
		echo "zsh_simple_abbreviations key '${key}' contains non-alphanumeric characters."
		return 1
	fi
	;;

--unset)
	if [[ $# -ne 2 ]]; then
		echo "zsh_simple_abbreviations unset sub-command requires only a key."
		return 1
	fi

	local key=${2}

	if [[ ${key} =~ ${KEY_REGEX} ]]; then
		unset "ZSH_SIMPLE_ABBREVIATIONS[${key}]"
	else
		echo "zsh_simple_abbreviations key '${key}' contains non-alphanumeric characters."
		return 1
	fi
	;;

--list)
	if [[ $# -ne 1 ]]; then
		echo "zsh_simple_abbreviations list sub-command takes no arguments."
		return 1
	fi

	if [[ ${#ZSH_SIMPLE_ABBREVIATIONS[@]} -eq 0 ]]; then
		echo "No abbreviations set."
		return 0
	fi

	for key in ${(k)ZSH_SIMPLE_ABBREVIATIONS}; do
		echo "${key} -> ${ZSH_SIMPLE_ABBREVIATIONS[$key]}"
	done
	;;

*)
	echo "zsh_simple_abbreviations unrecognised sub-command."
	return 1
	;;
esac
